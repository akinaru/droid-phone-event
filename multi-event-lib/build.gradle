apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 1
        targetSdkVersion 22
        versionCode 1
        versionName "0.31"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}


def siteUrl = 'https://github.com/bertrandmartel/multi-event-library'
def gitUrl = 'https://github.com/bertrandmartel/multi-event-library.git'


group = 'fr.bmartel'
version = android.defaultConfig.versionName

bintray {
    user = "$System.env.BINTRAY_USER"
    key = "$System.env.BINTRAY_KEY"

    configurations = ['archives']

    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload

    pkg {
        repo = 'maven'
        name = 'multi-event-library'
        desc = 'Android library used to catch a range of event types (volume, screen state, incoming call, SMS, Wifi connection etc...)'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/bertrandmartel/multi-event-library/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        labels = ['multi-event', 'android', 'android-event']
        publicDownloadNumbers = true

        version {
            name = android.defaultConfig.versionName //Bintray logical version name
            vcsTag = android.defaultConfig.versionName

            gpg {
                sign = true
                passphrase = "$System.env.GPG_PASSPHRASE"
            }

            mavenCentralSync {
                sync = false
                //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = "$System.env.MAVEN_CENTRAL_USER_TOKEN" //OSS user token
                password = "$System.env.MAVEN_CENTRAL_PASSWORD" //OSS user password
                close = '1'
                //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                description 'Android library used to catch a range of event types (volume, screen state, incoming call, SMS, Wifi connection etc...)'
                packaging 'aar'
                name 'multi-event-library'
                url siteUrl
                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://github.com/bertrandmartel/multi-event-library/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id 'bertrandmartel'
                        name 'Bertrand Martel'
                        email 'bmartel.fr@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/bertrandmartel/multi-event-library.git'
                    developerConnection 'https://github.com/bertrandmartel/multi-event-library.git'
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
